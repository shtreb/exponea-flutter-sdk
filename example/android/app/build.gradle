plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.exponea.example"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        config {
            // Set the parameters based on the actual signing information.
            storeFile file('Exponea.jks')
            storePassword 'Exponea'
            keyPassword 'Exponea'
            keyAlias 'key0'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "messagingService"

    productFlavors {
        gms {
            dimension = "messagingService"
        }
        hms {
            dimension = "messagingService"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "com.android.support:multidex:2.0.1"
    implementation "com.exponea.sdk:sdk:3.4.0"
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.google.code.gson:gson:2.8.9'

    gmsImplementation "com.google.firebase:firebase-messaging:23.0.0"
    hmsImplementation "com.huawei.agconnect:agconnect-core:1.5.2.300"
    hmsImplementation "com.huawei.hms:push:6.1.0.300"

    // tests :
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.exponea.sdk:sdk:3.4.0'
    testImplementation 'com.google.code.gson:gson:2.8.6'
}
